#Область СлужебныеПроцедурыИФункции

Процедура ОтправитьСообщения() Экспорт
	
	IDBot = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	IDGroup = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Ссылка,
				   |	ВКМ_УведомленияТелеграмБоту.ТекстСообщения КАК ТекстСообщения
				   |ИЗ
				   |	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту
				   |ГДЕ
				   |	НЕ ВКМ_УведомленияТелеграмБоту.ПометкаУдаления";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;

	Соединение = Новый HTTPСоединение("api.telegram.org", 443, , , , Истина);
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Не Выборка.ТекстСообщения = "" Тогда

			Заголовки = Новый Соответствие;
			Заголовки.Вставить("Content-Type", "application/json");

			Запрос = Новый HTTPЗапрос("/bot" + IDBot + "/sendMessage", Заголовки);

			Сообщение = Новый Структура;
			Сообщение.Вставить("chat_id", IDGroup);
			Сообщение.Вставить("text", Выборка.ТекстСообщения);

			ЗаписьJSON = Новый ЗаписьJSON;
			ЗаписьJSON.УстановитьСтроку();
			ЗаписатьJSON(ЗаписьJSON, Сообщение);
			СообщениеJSON = ЗаписьJSON.Закрыть();

			Запрос.УстановитьТелоИзСтроки(СообщениеJSON);
			Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);

			Если Ответ.КодСостояния = 200 Тогда
				СообщенияОбъект = Выборка.Ссылка.ПолучитьОбъект();
				СообщенияОбъект.Удалить();
			Иначе
				Сообщить("Хьюстон у нас проблемы");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти