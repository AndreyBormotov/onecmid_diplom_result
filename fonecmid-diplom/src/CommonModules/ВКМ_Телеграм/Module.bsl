#Область СлужебныеПроцедурыИФункции

Процедура ОтправитьСообщения() Экспорт
	
	IDBot = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	IDGroup = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	УведомленияТелеграмБоту.Ссылка КАК Ссылка,
	|	УведомленияТелеграмБоту.ТекстСообщения КАК ТекстСообщения
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграмБоту КАК УведомленияТелеграмБоту
	|ГДЕ
	|	НЕ УведомленияТелеграмБоту.ПометкаУдаления";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	МассивДляУдаления = Новый Массив;
	
	Попытка
		Соединение = Новый HTTPСоединение("api.telegram.org", 443, , , , Истина);
		
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("Content-Type", "application/json");
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.ТекстСообщения) Тогда
				ЗапросHTTP = Новый HTTPЗапрос("/bot" + IDBot + "/sendMessage", Заголовки);
	
				Сообщение = Новый Структура;
				Сообщение.Вставить("chat_id", IDGroup);
				Сообщение.Вставить("text", Выборка.ТекстСообщения);
	
				ЗаписьJSON = Новый ЗаписьJSON;
				ЗаписьJSON.УстановитьСтроку();
				ЗаписатьJSON(ЗаписьJSON, Сообщение);
				СообщениеJSON = ЗаписьJSON.Закрыть();
	
				ЗапросHTTP.УстановитьТелоИзСтроки(СообщениеJSON);
				Ответ = Соединение.ВызватьHTTPМетод("POST", ЗапросHTTP);
				
				Если Ответ.КодСостояния = 200 Тогда
					МассивДляУдаления.Добавить(Выборка.Ссылка);
				Иначе
					ТекстОшибки = "Ошибка отправки уведомления в Telegram. Код: " + Ответ.КодСостояния + 
						". Ответ: " + Ответ.ПолучитьТелоКакСтроку();
					ЗаписьЖурналаРегистрации("ОШИБКА.ВКМ_Телеграм", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;
		
		Если МассивДляУдаления.Количество() > 0 Тогда
			УдалитьОбъекты(МассивДляУдаления);
		КонецЕсли;	
		
	Исключение
		
		ТекстОшибки = "Критическая ошибка при работе с Telegram API: " + ОписаниеОшибки();
		ЗаписьЖурналаРегистрации("ОШИБКА.ВКМ_Телеграм", УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
	
	КонецПопытки;

КонецПроцедуры

#КонецОбласти