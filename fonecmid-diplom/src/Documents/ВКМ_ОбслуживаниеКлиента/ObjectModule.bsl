#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//Получаем условия оплаты сотруднику за работу
	УсловияОплатыСотрудника = ВКМ_Служебный.ПолучитьУсловияОплаты(Дата, Сотрудник);
	
	//Проверка установленных условий оплаты для сотрудника
	Если НЕ УсловияОплатыСотрудника.Пустой() Тогда
		
		//Получаем процент оплаты сотрудника за работу
		Выборка = УсловияОплатыСотрудника.Выбрать();
		Выборка.Следующий();
		ОплачиваемыйПроцент = Выборка.ПроцентОтРабот;
		
		//Проверка корректности даты проведения работ и срока договора
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВидДоговора")
			= Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда

			ДатаДействияДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор,
				"ВКМ_НачалоДействияДоговора, ВКМ_ОкончаниеДействияДоговора");

			Если ДатаДействияДоговора.ВКМ_НачалоДействияДоговора > ДатаПроведенияРабот Тогда
				Отказ = Истина;
				ОбщегоНазначения.СообщитьПользователю(
				"Дата проведения работ не может быть меньше даты начала действия договора");
			КонецЕсли;

			Если ДатаДействияДоговора.ВКМ_ОкончаниеДействияДоговора < ДатаПроведенияРабот Тогда
				Отказ = Истина;
				ОбщегоНазначения.СообщитьПользователю(
				"Дата проведения работ не может быть больше даты окончания действия договора");
			КонецЕсли;

			//Получаем стоимость часа работы по договору
			СтоимостьЧасаРаботы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВКМ_СтоимостьЧасаРаботы");
	
			// регистр ВКМ_ВыполненныеКлиентуРаботы
			Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
			
			// регистр ВКМ_ВыполненныеСотрудникомРаботы
			Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;

			Для Каждого Строка Из ВыполненныеРаботы Цикл

				Движение1 = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
				Движение1.Период = Дата;
				Движение1.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение1.Клиент = Клиент;
				Движение1.Договор = Договор;
				Движение1.Сотрудник = Сотрудник;
				Движение1.ОписаниеРабот = Строка.ОписаниеРабот;
				Движение1.КоличествоЧасов = Строка.ЧасыКОплатеКлиенту;
				
				СуммаКОплате = Движение1.КоличествоЧасов * СтоимостьЧасаРаботы;
				
				Движение1.СуммаКОплате = СуммаКОплате;
				
				Движение2 = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
				Движение2.Период = Дата;
				Движение2.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение2.Сотрудник = Сотрудник;
				Движение2.ЧасовКОплате = Строка.ЧасыКОплатеКлиенту;
				
				
				
				Движение2.СуммаКОплате = СуммаКОплате * ОплачиваемыйПроцент / 100;
				Движение2.ОписаниеРабот = Строка.ОписаниеРабот;
				
			КонецЦикла;

		Иначе
			
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Необходимо выбрать договор с видом ""Абонентское обслуживание");
		
		КонецЕсли;
		
	Иначе
		
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
			"Сотруднику %1 на дату документа не установлены условия оплаты", Сотрудник));
			
	КонецЕсли;
	
КонецПроцедуры


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда

		Если Ссылка.Пустая() Тогда
			СообщениеБота = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
			СообщениеБота.ТекстСообщения = СтрШаблон("Новый заказ №%1 от %2
													 |Клиент: %3
													 |Дата: %4
													 |Время начала: %5
													 |Время окончания: %6
													 |Специалист: %7
													 |Комментарий: %8",
													 Номер, 
													 Дата, 
													 Клиент, 
													 Формат(ДатаПроведенияРабот, "ДЛФ=DD;"), 
													 Формат(ВремяНачалаРабот, "ДФ=HH:mm;"), 
													 Формат(ВремяОкончанияРабот, "ДФ=HH:mm;"), 
													 Сотрудник,
													 Комментарий);
			СообщениеБота.Записать();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти