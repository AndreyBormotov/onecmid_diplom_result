#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВидДоговора") = 
			Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
		
		ДатаДействияДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор, 
														"ВКМ_НачалоДействияДоговора, ВКМ_ОкончаниеДействияДоговора");
		
		Если ДатаДействияДоговора.ВКМ_НачалоДействияДоговора > ДатаПроведенияРабот Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Дата проведения работ не может быть меньше даты начала действия договора");
		КонецЕсли;
		Если ДатаДействияДоговора.ВКМ_ОкончаниеДействияДоговора < ДатаПроведенияРабот Тогда
			Отказ = Истина;
			ОбщегоНазначения.СообщитьПользователю("Дата проведения работ не может быть больше даты окончания действия договора");
		КонецЕсли;
	КонецЕсли;
	
	// регистр ВКМ_ВыполненныеРаботыКлиенту
	Движения.ВКМ_ВыполненныеРаботыКлиенту.Записывать = Истина;
	Движение = Движения.ВКМ_ВыполненныеРаботыКлиенту.Добавить();
	Движение.Период = Дата;
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Клиент = Клиент;
	Движение.Договор = Договор;
	Движение.КоличествоЧасов = ВыполненныеРаботы.Итог("ЧасыКОплатеКлиенту");
	
	СтоимостьЧасаРаботы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВКМ_СтоимостьЧасаРаботы");
	Движение.СуммаКОплате = Движение.КоличествоЧасов * СтоимостьЧасаРаботы;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Ссылка.Пустая() Тогда
		СообщениеБота = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		СообщениеБота.ТекстСообщения = СтрШаблон("Новый заказ от %1
									|Клиент: %2
									|Дата: %3
									|Время начала: %4
									|Время окончания: %5
									|Специалист: %6
									|Комментарий: %7", 
									Дата,
									Клиент,
									Формат(ДатаПроведенияРабот, "ДЛФ=DD;"),
									Формат(ВремяНачалаРабот, "ДФ=HH:mm;"),
									Формат(ВремяОкончанияРабот, "ДФ=HH:mm;"),
									Специалист,
									Комментарий);
		СообщениеБота.Записать();
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти
