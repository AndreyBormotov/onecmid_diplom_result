#Область ОбработчикиСобытий
Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Если ВКМ_Служебный.ПроцентОплатыУстановлен() Тогда

		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВидДоговора")
			= Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда

			ДатаДействияДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Договор,
				"ВКМ_НачалоДействияДоговора, ВКМ_ОкончаниеДействияДоговора");

			Если ДатаДействияДоговора.ВКМ_НачалоДействияДоговора > ДатаПроведенияРабот Тогда
				Отказ = Истина;
				ОбщегоНазначения.СообщитьПользователю(
				"Дата проведения работ не может быть меньше даты начала действия договора");
			КонецЕсли;
			Если ДатаДействияДоговора.ВКМ_ОкончаниеДействияДоговора < ДатаПроведенияРабот Тогда
				Отказ = Истина;
				ОбщегоНазначения.СообщитьПользователю(
				"Дата проведения работ не может быть больше даты окончания действия договора");
			КонецЕсли;

			КоличествоЧасовРаботы = ВыполненныеРаботы.Итог("ЧасыКОплатеКлиенту");
			СтоимостьЧасаРаботы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВКМ_СтоимостьЧасаРаботы");
			СуммаКОплате = КоличествоЧасовРаботы * СтоимостьЧасаРаботы;
	
		// регистр ВКМ_ВыполненныеКлиентуРаботы
			Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
			Движение1 = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
			Движение1.Период = Дата;
			Движение1.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение1.Клиент = Клиент;
			Движение1.Договор = Договор;
			Движение1.КоличествоЧасов = КоличествоЧасовРаботы;
			Движение1.СуммаКОплате = СуммаКОплате;

		// регистр ВКМ_ВыполненныеСотрудникомРаботы
			Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
			Движение2 = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
			Движение2.Период = Дата;
			Движение2.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение2.Сотрудник = Специалист;
			Движение2.ЧасовКОплате = КоличествоЧасовРаботы;
			Движение2.СуммаКОплате = СуммаКОплате;
		КонецЕсли;
	Иначе
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Сотруднику %1 не установлены условия оплаты", Сотрудник));
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если Ссылка.Пустая() Тогда
		СообщениеБота = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
		СообщениеБота.ТекстСообщения = СтрШаблон("Новый заказ от %1
												 |Клиент: %2
												 |Дата: %3
												 |Время начала: %4
												 |Время окончания: %5
												 |Специалист: %6
												 |Комментарий: %7", Дата, Клиент, Формат(ДатаПроведенияРабот, "ДЛФ=DD;"),
			Формат(ВремяНачалаРабот, "ДФ=HH:mm;"), Формат(ВремяОкончанияРабот, "ДФ=HH:mm;"), Специалист, Комментарий);
		СообщениеБота.Записать();
	КонецЕсли;

КонецПроцедуры
#КонецОбласти