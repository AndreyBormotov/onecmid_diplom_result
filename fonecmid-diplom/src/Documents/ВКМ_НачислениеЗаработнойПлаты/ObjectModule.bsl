
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПериодРегистрации = НачалоМесяца(Дата);
	НачалоБазовогоПериодаОтпуска = ДобавитьМесяц(ПериодРегистрации, -12);
	КонецБазовогоПериодаОтпуска = ПериодРегистрации -1;
	
	ТЗСписокСотрудников = СписокСотрудников.Выгрузить();
	ТЗСписокСотрудников.Свернуть("Сотрудник");
	МассивСотрудников = ТЗСписокСотрудников.ВыгрузитьКолонку("Сотрудник");
	ВыборкаУсловияОплатыСотрудников = ВКМ_Служебный.ПолучитьУсловияОплаты(Дата, МассивСотрудников).Выбрать();
	
	//Предварительное заполнение для расчетов	
	Движения.ВКМ_ОсновныеНачисления.Записывать = Истина;
	
	Для Каждого Строка Из СписокСотрудников Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Строка);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.ПериодДействияНачало = Строка.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
		
		Если Строка.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.НайтиПоНаименованию("Отпуск", Истина) Тогда
			Движение.БазовыйПериодНачало = НачалоБазовогоПериодаОтпуска;
			Движение.БазовыйПериодКонец = КонецБазовогоПериодаОтпуска;
		КонецЕсли;
		
		Отбор = Новый Структура("Сотрудник", Строка.Сотрудник);
		Если ВыборкаУсловияОплатыСотрудников.НайтиСледующий(Отбор) Тогда
			
			Если ВыборкаУсловияОплатыСотрудников.Оклад = 0 Тогда
				ВызватьИсключение(СтрШаблон("На дату документа сотруднику %1 установлен нулевой оклад, начисление оплаты невозможно", Строка.Сотрудник));
				Отказ = Истина;
			КонецЕсли;
			
			Движение.Оклад = ВыборкаУсловияОплатыСотрудников.Оклад;
			
		Иначе
			ВызватьИсключение(СтрШаблон("На дату документа не заполнены условия оплаты для сотрудника: %1", Строка.Сотрудник));
			Отказ = Истина;
		КонецЕсли;
		
		ВыборкаУсловияОплатыСотрудников.Сбросить();
		
	КонецЦикла;
	
	//Заполнение сторно записей
	СторноЗаписи = Движения.ВКМ_ОсновныеНачисления.ПолучитьДополнение();
	Для Каждого Запись Из СторноЗаписи Цикл
		
		Движение = Движения.ВКМ_ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Запись);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.ПериодДействияНачало = Запись.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Запись.ПериодДействияКонецСторно;
		Движение.Сторно = Истина;
		
	КонецЦикла;
	
	Движения.Записать();
	
	//Расчет результата начислений
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК РабочихДней,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия, 0) КАК ОтработанныхДней,
	|	ВКМ_ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.РезультатБаза, 0) +
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.РезультатБаза, 0) КАК НачисленияВБазовомПериоде,
	|	ЕСТЬNULL(ВКМ_ОсновныеНачисленияДанныеГрафика.ЗначениеБазовыйПериод, 0) КАК ДнейВБазовомПериоде
	|ПОМЕСТИТЬ ВТ_ДанныеГрафика
	|ИЗ
	|	РегистрРасчета.ВКМ_ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор
	|	И ВидРасчета В (&ВидыРасчета)) КАК ВКМ_ОсновныеНачисленияДанныеГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ОсновныеНачисления(&Измерения, &Измерения,,
	|			Регистратор = &Регистратор
	|		И ВидРасчета В (&ВидыРасчета)) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления
	|		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ОсновныеНачисления.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_ОсновныеНачисления.БазаВКМ_ДополнительныеНачисления(&Измерения, &Измерения,,
	|			Регистратор = &Регистратор
	|		И ВидРасчета В (&ВидыРасчета)) КАК ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления
	|		ПО ВКМ_ОсновныеНачисленияДанныеГрафика.НомерСтроки = ВКМ_ОсновныеНачисленияБазаВКМ_ДополнительныеНачисления.НомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.Оклад КАК Оклад,
	|	ВТ_ДанныеГрафика.РабочихДней КАК РабочихДней,
	|	ВТ_ДанныеГрафика.ОтработанныхДней КАК ОтработанныхДней,
	|	ВТ_ДанныеГрафика.НачисленияВБазовомПериоде КАК НачисленияВБазовомПериоде,
	|	ВТ_ДанныеГрафика.ДнейВБазовомПериоде КАК ДнейВБазовомПериоде
	|ИЗ
	|	ВТ_ДанныеГрафика КАК ВТ_ДанныеГрафика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&ПериодРегистрации, Сотрудник В
	|			(ВЫБРАТЬ
	|				ВТ_ДанныеГрафика.Сотрудник КАК Сотрудник
	|			ИЗ
	|				ВТ_ДанныеГрафика КАК ВТ_ДанныеГрафика)) КАК ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ПО ВТ_ДанныеГрафика.Сотрудник = ВКМ_УсловияОплатыСотрудниковСрезПоследних.Сотрудник";

	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);

	ВидыРасчета = Новый Массив;
	ВидыРасчета.Добавить(ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Оклад);
	ВидыРасчета.Добавить(ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск);
	Запрос.УстановитьПараметр("ВидыРасчета", ВидыРасчета);

	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);

	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Для Каждого Движение Из Движения.ВКМ_ОсновныеНачисления Цикл

		Выборка.Сбросить();
		Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
		ЗаполнитьЗначенияСвойств(Движение, Выборка);

		Если Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.Отпуск Тогда

			Движение.Результат = ?(Выборка.ДнейВБазовомПериоде <> 0, Выборка.НачисленияВБазовомПериоде
				/ Выборка.ДнейВБазовомПериоде * Выборка.ОтработанныхДней, 0);
				
			Продолжить;
			
		КонецЕсли;

		Движение.Результат = ?(Выборка.РабочихДней <> 0, Выборка.Оклад / Выборка.РабочихДней
			* Выборка.ОтработанныхДней, 0);

		Если Движение.Сторно Тогда
			Движение.Результат = -Движение.Результат;
			Движение.ОтработанныхДней = -Движение.ОтработанныхДней;
		КонецЕсли;

	КонецЦикла;
	
	Движения.ВКМ_ОсновныеНачисления.Записать(, Истина);
	
КонецПроцедуры