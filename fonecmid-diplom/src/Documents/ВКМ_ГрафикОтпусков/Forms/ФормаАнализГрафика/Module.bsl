#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АвтоЗаголовок = Ложь;
	Заголовок = СтрШаблон("Анализ графика отпусков на %1 год", Формат(Параметры.Год, "ДФ=гггг"));
	
	ДиаграммаГанта.Обновление = Ложь;
	ДиаграммаГанта.Очистить();
	ДиаграммаГанта.АвтоОпределениеПолногоИнтервала = Ложь; 
	ДиаграммаГанта.УстановитьПолныйИнтервал(НачалоГода(Параметры.Год), КонецГода(Параметры.Год)); 
	ДиаграммаГанта.ОбластьЛегенды.Расположение = РасположениеЛегендыДиаграммы.Нет;
	
	ТаблицаОтпусков = ПолучитьИзВременногоХранилища(Параметры.АдресВоВременномХранилище);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтпусков.Сотрудник КАК Сотрудник,
	|	ТаблицаОтпусков.ДатаНачала КАК ДатаНачала,
	|	ТаблицаОтпусков.ДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ТаблицаОтпусков
	|ИЗ
	|	&ТаблицаОтпусков КАК ТаблицаОтпусков
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ТаблицаОтпусков.Сотрудник КАК Сотрудник,
	|	ВТ_ТаблицаОтпусков.ДатаНачала КАК ДатаНачала,
	|	ВТ_ТаблицаОтпусков.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	ВТ_ТаблицаОтпусков КАК ВТ_ТаблицаОтпусков
	|ИТОГИ
	|	МАКСИМУМ(ДатаНачала) КАК ДатаНачала,
	|	МАКСИМУМ(ДатаОкончания) КАК ДатаОкончания
	|ПО
	|	Сотрудник";
	Запрос.УстановитьПараметр("ТаблицаОтпусков", ТаблицаОтпусков);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
	Пока РезультатЗапроса.Следующий() Цикл
		Точка = ДиаграммаГанта.УстановитьТочку(РезультатЗапроса.Сотрудник);
		Точка.Текст = РезультатЗапроса.Сотрудник.Наименование;
		
		Серия = ДиаграммаГанта.УстановитьСерию("Отпуска");
		Серия.Цвет = WebЦвета.Аквамарин;
		Серия.ШтриховкаПерекрывающихсяИнтервалов = Истина;
		Серия.ДополнительныйЦвет = WebЦвета.Персиковый;
		
		Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ВсегоДнейОтпуска = 0;
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Интервал = Значение.Добавить();
			Интервал.Начало = ВыборкаДетальныеЗаписи.ДатаНачала;
			Интервал.Конец = ВыборкаДетальныеЗаписи.ДатаОкончания;
			
			ДнейИнтервала = 1 + (Интервал.Конец - Интервал.Начало) /24 /60 /60;
			ВсегоДнейОтпуска = ВсегоДнейОтпуска + ДнейИнтервала;
		
		КонецЦикла;
		
		Если ВсегоДнейОтпуска > 28 Тогда
				Значение.ЦветФона = WebЦвета.Розовый;
		КонецЕсли;
		
	КонецЦикла;
	
	ДиаграммаГанта.Обновление = Истина;

КонецПроцедуры

#КонецОбласти