#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ПериодПриИзмененииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	СоздатьДокументыНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере	
Процедура ПериодПриИзмененииНаСервере()

	СписокДоговоров.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	|	ДоговорыКонтрагентов.Ссылка КАК Договор
	|ПОМЕСТИТЬ ВТ_Договоры
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
	|	И ДоговорыКонтрагентов.ВКМ_НачалоДействияДоговора < &КонецПериода
	|	И (ДоговорыКонтрагентов.ВКМ_ОкончаниеДействияДоговора >= &НачалоПериода
	|	ИЛИ ДоговорыКонтрагентов.ВКМ_ОкончаниеДействияДоговора = ДАТАВРЕМЯ(1, 1, 1))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Договор КАК Договор,
	|	РеализацияТоваровУслуг.Договор.ВидДоговора КАК ДоговорВидДоговора
	|ПОМЕСТИТЬ ВТ_Реализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РеализацияТоваровУслуг.Проведен
	|	И
	|		РеализацияТоваровУслуг.Договор.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Договоры.Контрагент КАК Контрагент,
	|	ВТ_Договоры.Договор КАК Договор,
	|	ВТ_Реализации.Ссылка КАК ДокументРеализацииУслуг
	|ИЗ
	|	ВТ_Договоры КАК ВТ_Договоры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Реализации КАК ВТ_Реализации
	|		ПО ВТ_Договоры.Договор = ВТ_Реализации.Договор";

	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);

	РезультатЗапроса = Запрос.Выполнить();

	Выборка = РезультатЗапроса.Выбрать();

	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СписокДоговоров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыНаСервере()
	
	Для Каждого Строка Из СписокДоговоров Цикл
		
		Если ЗначениеЗаполнено(Строка.ДокументРеализацииУслуг) Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
		ДокументОбъект.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Строка.Договор, "Организация");
		ДокументОбъект.Дата = КонецМесяца(Период.ДатаОкончания);
		ДокументОбъект.Договор = Строка.Договор;
		ДокументОбъект.Контрагент = Строка.Контрагент;
		ДокументОбъект.ВКМ_ВыполнитьАвтозаполнение();
		ДокументОбъект.ПроверитьЗаполнение();
		ДокументОбъект.Записать();
	КонецЦикла;

КонецПроцедуры

#КонецОбласти